//------------------------------------------------------------------------------
//                                                      (c) Корпорация Галактика
// Заработная плата по отделу
// Табличный объект подразделений
//------------------------------------------------------------------------------

#include AtlProtocol.vih

Interface Depart;

// Подразделения
Table struct mtDepart
(
  Id:   byte,
  Name: s100
)
With index
(
  DepId  = Id (unique),
  ByName = Name
);

Create view from mtDepart, mtDepart mtDepart2;

property Id:   byte absolute mtDepart.Id;
property Name: s100 absolute mtDepart.Name;

index DepId  auto;
index ByName auto;

public function GetName(_Id: byte): string;
{
  result := '';

  if (GetFirst mtDepart2 where (( _Id == mtDepart2.Id )) = tsOk)
    result := mtDepart2.Name;
}

private function GenerateNewId: byte;
{
  result := 1;

  if (GetLast mtDepart2 ordered by index DepId = tsOk)
    result := mtDepart2.Id + 1;
}

function IModify.doInsert: word;
{
  if (mtDepart.Id = 0)
    mtDepart.Id := GenerateNewId;

  result := insert current mtDepart;
  oWage.DataChanged := true;
}

function IModify.doUpdate: word;
{
  result := update current mtDepart;
  oWage.DataChanged := true;
}

function IModify.doDelete: word;
{
  result := delete current mtDepart;
  oWage.DataChanged := true;
}

implementation IModify.doFlush  abstract;

constructor Init;
{
  result := true;
  delete all mtDepart;
}

public procedure Clear(_wage: IWageView = NullRef);
{
  oWage := _wage;
  delete all mtDepart2;
}

public function SaveXml(_e: IDomNode): IDomNode;
{
  var xnode: IDomNode
  xnode := _e.AddNode('rok:Depart');
  xnode.AddAttr('rok:Id='   + string(Id));
  xnode.AddAttr('rok:Name=' + Name);
  result := xnode;
}

public function LoadXml(_e: IDomNode): boolean;
{
  ClearBuffer(#mtDepart);
  if _e.GetFirstAttr
    do
    {
      case _e.FndAttr.Name of
        'rok:NRec': Id   := GetId(cgDepart, comp(_e.FndAttr.Value));
        'rok:Id':   Id   := GetId(cgDepart, comp(_e.FndAttr.Value));
        'rok:Name': Name := _e.FndAttr.Value;
      end;
    }
    while (_e.GetNextAttr);

  if (GetFirst mtDepart2 where (( mtDepart.Id == mtDepart2.Id )) <> tsOk)
  {
    insert current mtDepart;
    oWage.Log(APE_INFO, string(Id) + '. ' + Name);
  }

  result := true;
}

public function SaveRok: boolean;
{
  result := true;
  StartNewRec;
  WriteByte(Id);
  WriteStr(Name);
}

public function LoadRok: boolean;
{
  result := false;

  if CheckNewRec
  {
    ClearBuffer(#mtDepart);
    Id   := ReadByte;
    Name := ReadStr;
    if (GetFirst mtDepart2 where (( mtDepart.Id == mtDepart2.Id )) <> tsOk)
    {
      insert current mtDepart;
      oWage.Log(APE_INFO, string(Id) + '. ' + Name);
    }
    result := true;
  }
}

end.
