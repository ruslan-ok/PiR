//------------------------------------------------------------------------------
//                                                      (c) Корпорация Галактика
// Заработная плата по отделу
// Табличный объект должностей
//------------------------------------------------------------------------------

Interface Post;

// Должности
Table struct mtPost
(
  NRec: comp,
  Code: s50,
  Name: s100
)
With index
(
  PostId = NRec (surrogate),
  ByCode = Code,
  ByName = Name
);

Create view from mtPost, mtPost mtPost2;

property NRec: comp absolute mtPost.NRec;
property Code: s50  absolute mtPost.Code;
property Name: s100 absolute mtPost.Name;

index PostId auto;
index ByCode auto;
index ByName auto;

function IModify.doInsert : word;
{
  result := insert current mtPost;
}

function IModify.doUpdate : word;
{
  result := update current mtPost;
}

function IModify.doDelete : word;
{
  result := delete current mtPost;
}

implementation IModify.doFlush  abstract;

constructor Init;
{
  result := true;
  delete all mtPost;
}

public procedure Clear;
{
  delete all mtPost2;
}

public function Load(_e: IDomNode): boolean;
{
  ClearBuffer(#mtPost);
  if _e.GetFirstAttr
    do
    {
      case _e.FndAttr.Name of
        'rok:NRec': NRec := comp(_e.FndAttr.Value);
        'rok:Code': Code := _e.FndAttr.Value;
        'rok:Name': Name := _e.FndAttr.Value;
      end;
    }
    while (_e.GetNextAttr);
  insert current mtPost;
  result := true;
}

public function Save(_e: IDomNode): IDomNode;
{
  var xnode: IDomNode
  xnode := _e.AddNode('rok:Post');
  xnode.AddAttr('rok:NRec=' + string(NRec,0,0));
  xnode.AddAttr('rok:Code=' + Code);
  xnode.AddAttr('rok:Name=' + Name);
  result := xnode;
}

end.
