//------------------------------------------------------------------------------
//                                                      (c) Корпорация Галактика
// Заработная плата по отделу
// Окно редактирования назначения сотрудника
//------------------------------------------------------------------------------

Window wnAppoint 'Назначение сотрудника';
  show at(,, 80, 10);

Screen scAppoint ('',, sci178Esc);
  table mtAppoint;
Fields
  mtStruct.Name: skip;
  mtAppoint.TabNum: NoProtect;
  mtAppoint.dBeg  : NoProtect;
  mtAppoint.dEnd  : NoProtect;
  mtDepart.GetName(mtAppoint.Depart) ('',, sci1378Esc): Protect;
  mtPost.GetName(mtAppoint.Post)   ('',, sci1378Esc): Protect;
  mtAppoint.Salary (''): [13.2,'\2p[|-]366`666`666~99'], NoProtect;
  mtAppoint.Currency: NoProtect;
  mtAppoint.TaxDed: NoProtect;
  mtAppoint.Info: NoProtect;
<<

   `Сотрудник`    .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
                                          `Табельный номер` .@@@@@@@@@@@@
   `Назначение с` .@@@@@@@@@@@@               `Контракт до` .@@@@@@@@@@@@
   `Отдел`        .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Должность`    .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Оклад`        .@@@@@@@@@@@@ .@@@@@@@@          `Льгота` .@@@@@@@@@@@@
   `Коммент`      .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
End;

End;

TableEvent
  table mtAppoint;

cmInsertRecord:
  insert current mtAppoint;

cmUpdateRecord:
  update current mtAppoint;

cmDeleteRecord:
  if (message('Удалить назначение?', Confirmation + YesNo) = cmYes)
    delete current mtAppoint;

end;

private procedure DoPick;
{
  case CurField of

    #mtDepart.Name:
    {
      cSelectedDep := mtAppoint.Depart;
      RunWindowModal(wnPickDepart);
      if (cSelectedDep <> 0)
        set mtAppoint.Depart := cSelectedDep;
    }

    #mtPost.Name:
    {
      cSelectedPost := mtAppoint.Post;
      RunWindowModal(wnPickPost);
      if (cSelectedPost <> 0)
        set mtAppoint.Post := cSelectedPost;
    }

  end;
}

private procedure DoDel;
{
  case CurField of
    #mtDepart.Name: set mtAppoint.Depart := 0;
    #mtPost.Name:   set mtAppoint.Post   := 0;
  end;
}

private procedure RunAppWin;
{
  RunWindowModal(wnAppoint);
}

procedure GetCurAppoint(_mode: byte);
{
  CurApp_dBeg := ZeroDate;
  CurApp_dEnd := ZeroDate;
  CurApp_Salary := 0;
  CurApp_Currency := '';
  CurApp_TaxDed := 0;
  set CurApp_Depart := 0;
  set CurApp_Post := 0;

  if (_mode = 2)
  {
    PushPos(#mtAppoint);
    if (GetFirst mtAppoint where (( mtPerson.Id    == mtAppoint.Person and
                                    mtPeriod.dBeg >>= mtAppoint.dBeg and
                                    ((mtAppoint.dEnd = ZeroDate) or (mtAppoint.dEnd > mtPeriod.dBeg)) )) = tsOk)
    {
      CurApp_dBeg := mtAppoint.dBeg;
      CurApp_dEnd := mtAppoint.dEnd;
      CurApp_Salary := mtAppoint.Salary;
      CurApp_Currency := mtAppoint.Currency;
      CurApp_TaxDed := mtAppoint.TaxDed;
      set CurApp_Depart := mtAppoint.Depart;
      set CurApp_Post := mtAppoint.Post;
    }
    PopPos(#mtAppoint);
  }
}
