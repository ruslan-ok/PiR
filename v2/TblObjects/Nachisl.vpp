//------------------------------------------------------------------------------
//                                                      (c) Корпорация Галактика
// Заработная плата по отделу
// Окно редактирования начисления
//------------------------------------------------------------------------------

Window wnNachisl 'Начисление';
  show at(,, 80, 13);

Screen scNachisl ('',, sci178Esc);
  table mtNachisl;
Fields
  mtStruct.Name: skip;
  mtNachisl.dPeriod:  ['MM.YYYY'], skip;
  mtNachisl.Sort:         noprotect;
  [sPayTitle] mtNachisl.GetTitleName() ('',, sci1378Esc): protect;
  mtNachisl.Payed:        noprotect;
  mtNachisl.Value: [13.2,'\2p[|-]366`666`666~99'], noprotect;
  mtNachisl.Currency: [list cgBYN 'BYN', cgUSD 'USD'], protect;
  mtNachisl.inBYN:  skip;
  mtNachisl.Rate:   [13.4,'\4p[|-]366`666~9999'], noprotect;
  mtNachisl.inUSD:  skip;
  mtNachisl.Info:   noprotect;
<<

   `Сотрудник`    .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Период`       .@@@@@@@@@@@@              `Номер п/п`    .@@@@@@@@@@@@
   `Название`     .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Дата выплаты` .@@@@@@@@@@@@
   `Сумма`        .@@@@@@@@@@@@ .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

   `Сумма в BYN`  .@@@@@@@@@@@@ `Курс` .@@@@@@@@@@@@  `USD` .@@@@@@@@@@@@
   `Комментарий`  .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
End;

End;

WindowEvent wnNachisl;

cmPick:
  if (CurField = #sPayTitle)
  {
    cSelectedPayTitle := mtNachisl.Title;
    RunWindowModal(wnPickPayTitle);
    if (cSelectedPayTitle <> 0)
      set mtNachisl.Title := cSelectedPayTitle;
  }

cmDelOnProtect:
  if (CurField = #sPayTitle)
    set mtNachisl.Title := 0;

End;

TableEvent
  table mtNachisl;

cmInsertRecord:
  insert current mtNachisl;

cmUpdateRecord:
  update current mtNachisl;

cmDeleteRecord:
  if (message('Удалить начисление?', Confirmation + YesNo) = cmYes)
    delete current mtNachisl;

end;
