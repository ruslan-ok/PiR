//------------------------------------------------------------------------------
//                                                      (c) Корпорация Галактика
// Заработная плата по отделу
// Окно редактирования должности
//------------------------------------------------------------------------------

var cSelectedPost: byte;
var sPost: string;

Window wnPost 'Должность';
  show at(,, 80, 5);

Screen scPost ('',, sci178Esc);
  table mtPost;
Fields
  mtPost.Code (''): noprotect;
  mtPost.Name (''): noprotect;
<<

   `Код`          .@@@@@@@@@@@@
   `Наименование` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
End;

End;

TableEvent
  table mtPost;

cmInsertRecord:
  insert current mtPost;

cmUpdateRecord:
  update current mtPost;

cmDeleteRecord:
  if (GetFirst mtAppoint where (( mtPost.Id == mtAppoint.Post )) = tsOk)
    message('Удаление невозможно, так как есть назначения на эту должность.');
  else
    if (message('Удалить должность?', Confirmation + YesNo) = cmYes)
      delete current mtPost;

end;


Window wnPickPost 'Выбор должности' ('',, sci1478EnEsc) DoAccept;
  show at(,, 60, 15);

Browse brPost;
  table mtPost;
Fields
  mtPost.Code 'Код':          [20], protect;
  mtPost.Name 'Наименование': [40], protect;
End;

End;

WindowEvent wnPickPost;

cmInit:
{
  PushPos(#mtPost);
  ResetBounds(#mtPost);

  if (GetFirst mtPost where (( cSelectedPost == mtPost.Id )) = tsOk)
    RescanPanel(#mtPost);

  cSelectedPost := 0;
}

cmDefault:
{
  UpdateTable;
  cSelectedPost := mtPost.Id;
  set sPost := mtPost.Name;
}

cmDone:
{
  SetBounds(#mtPost);
  PopPos(#mtPost);
}

cmEdit:
  RunWindowModal(wnPost);

cmInsert:
  PutCommand(cmEdit);

End;
