//------------------------------------------------------------------------------
//                                                      (c) Корпорация Галактика
// Заработная плата по отделу
// Табличный объект наименований начислений и выплат
//------------------------------------------------------------------------------

#include AtlProtocol.vih

Interface PayTitle;

// Наименования начислений и выплат
Table struct mtPayTitle
(
  Id:   byte,
  Name: s100
)
With index
(
  ById = Id (unique),
  ByName = Name
);

Create view from mtPayTitle, mtPayTitle mtPayTitle2, mtPayTitle mtPayTitle3;

property Id:   byte absolute mtPayTitle.Id;
property Name: s100 absolute mtPayTitle.Name;

index ById   auto;
index ByName auto;

private function GenerateNewId: byte;
{
  //SiLogDll_LogDebug('GenerateNewId');
  if (GetLast mtPayTitle2 ordered by index ById <> tsOk)
    result := 1;
  else
    result := mtPayTitle2.Id + 1;
  //SiLogDll_LogDebug('= ' + string(result));
}

public function GetId(_title: string): byte;
{
  //SiLogDll_LogDebug('GetId(' + _title + ')');
  if (GetFirst mtPayTitle3 where (( _title == mtPayTitle3.Name )) <> tsOk)
  {
    //SiLogDll_LogDebug('not tsOk');
    ClearBuffer(#mtPayTitle3);
    mtPayTitle3.Name := _title;
    mtPayTitle3.Id := GenerateNewId;
    //SiLogDll_LogDebug('before insert ' + string(mtPayTitle3.Id) + '. ' + mtPayTitle3.Name);
    var res: word;
    res := insert current mtPayTitle3;
    if (res <> tsOk)
      oWage.Log(APE_ERROR, 'Ошибка добавления наименования начисления/выплаты: ' + string(mtPayTitle3.Id) + '. ' + mtPayTitle3.Name);
    else
      oWage.Log(APE_WARNING, 'Добавлено наименование начисления/выплаты: ' + string(mtPayTitle3.Id) + '. ' + mtPayTitle3.Name);
  }
  result := mtPayTitle3.Id;
  //SiLogDll_LogDebug('= ' + string(result));
}

public function GetTitle(_title: byte): string;
{
  result := '';

  if (GetFirst mtPayTitle where (( _title == mtPayTitle.Id )) = tsOk)
    result := mtPayTitle.Name;
}

function IModify.doInsert: word;
{
  result := insert current mtPayTitle;
  oWage.DataChanged := true;
}

function IModify.doUpdate: word;
{
  result := update current mtPayTitle;
  oWage.DataChanged := true;
}

function IModify.doDelete: word;
{
  result := delete current mtPayTitle;
  oWage.DataChanged := true;
}

implementation IModify.doFlush abstract;

constructor Init;
{
  result := true;
  delete all mtPayTitle;
}

public procedure Clear(_wage: IWageView = NullRef);
{
  oWage := _wage;
  delete all mtPayTitle;
}

public function SaveXml(_e: IDomNode): IDomNode;
{
  var xnode: IDomNode
  xnode := _e.AddNode('rok:Post');
  xnode.AddAttr('rok:Id='   + string(Id));
  xnode.AddAttr('rok:Name=' + Name);
  result := xnode;
}

public function LoadXml(_e: IDomNode): boolean;
{
  ClearBuffer(#mtPayTitle);
  if _e.GetFirstAttr
    do
    {
      case _e.FndAttr.Name of
        'rok:Id':   Id   := byte(_e.FndAttr.Value);
        'rok:Name': Name := _e.FndAttr.Value;
      end;
    }
    while (_e.GetNextAttr);

  if (GetFirst mtPayTitle2 where (( mtPayTitle.Id == mtPayTitle2.Id )) <> tsOk)
  {
    var res: word;
    res := insert current mtPayTitle;
    if (res <> tsOk)
      oWage.Log(APE_ERROR, 'Ошибка добавления наименования начисления/выплаты: ' + string(mtPayTitle3.Id) + '. ' + mtPayTitle3.Name);
    else
      oWage.Log(APE_WARNING, 'Добавлено наименование начисления/выплаты: ' + string(mtPayTitle3.Id) + '. ' + mtPayTitle3.Name);
    oWage.DataChanged := true;
  }
  result := true;
}

public function SaveRok: boolean;
{
  result := true;
  StartNewRec;
  WriteByte(Id);
  WriteStr(Name);
}

public function LoadRok: boolean;
{
  result := false;

  if CheckNewRec
  {
    ClearBuffer(#mtPayTitle);
    Id   := ReadByte;
    Name := ReadStr;
    if (GetFirst mtPayTitle2 where (( mtPayTitle.Id == mtPayTitle2.Id )) <> tsOk)
    {
      var res: word;
      res := insert current mtPayTitle;
      if (res <> tsOk)
        oWage.Log(APE_WARNING, 'Error on insert: ' + string(mtPayTitle.Id) + '. ' + mtPayTitle.Name);
      else
        oWage.Log(APE_INFO, string(mtPayTitle.Id) + '. ' + mtPayTitle.Name);
    }
    result := true;
  }
}

end.
