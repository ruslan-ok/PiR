//------------------------------------------------------------------------------
//                                                      (c) Корпорация Галактика
// Заработная плата по отделу
// Табличный объект должностей
//------------------------------------------------------------------------------

Interface Post;

// Должности
Table struct mtPost
(
  Id:   byte,
  Code: s50,
  Name: s100
)
With index
(
  PostId = Id (unique),
  ByCode = Code,
  ByName = Name
);

Create view from mtPost, mtPost mtPost2;

property Id:   byte absolute mtPost.Id;
property Code: s50  absolute mtPost.Code;
property Name: s100 absolute mtPost.Name;

index PostId auto;
index ByCode auto;
index ByName auto;

function IModify.doInsert : word;
{
  result := insert current mtPost;
}

function IModify.doUpdate : word;
{
  result := update current mtPost;
}

function IModify.doDelete : word;
{
  result := delete current mtPost;
}

implementation IModify.doFlush  abstract;

constructor Init;
{
  result := true;
  delete all mtPost;
}

public procedure Clear(_wage: IWageView = NullRef);
{
  oWage := _wage;
  delete all mtPost2;
}

public function LoadXml(_e: IDomNode): boolean;
{
  ClearBuffer(#mtPost);
  if _e.GetFirstAttr
    do
    {
      case _e.FndAttr.Name of
        'rok:NRec': Id   := GetId(cgPost, comp(_e.FndAttr.Value));
        'rok:Id':   Id   := GetId(cgPost, comp(_e.FndAttr.Value));
        'rok:Code': Code := _e.FndAttr.Value;
        'rok:Name': Name := _e.FndAttr.Value;
      end;
    }
    while (_e.GetNextAttr);
  insert current mtPost;
  result := true;
}

public function SaveXml(_e: IDomNode): IDomNode;
{
  var xnode: IDomNode
  xnode := _e.AddNode('rok:Post');
  xnode.AddAttr('rok:Id='   + string(Id));
  xnode.AddAttr('rok:Code=' + Code);
  xnode.AddAttr('rok:Name=' + Name);
  result := xnode;
}

public function LoadRok: boolean;
{
  result := false;

  if CheckNewRec
  {
    ClearBuffer(#mtPost);
    Id   := ReadByte;
    Code := ReadStr;
    Name := ReadStr;
    insert current mtPost;
    result := true;
  }
}

public function SaveRok: boolean;
{
  result := true;
  StartNewRec;
  WriteByte(Id);
  WriteStr(Code);
  WriteStr(Name);
}

end.
