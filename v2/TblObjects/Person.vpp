//------------------------------------------------------------------------------
//                                                      (c) Корпорация Галактика
// Заработная плата по отделу
// Окно редактирования сотрудника
//------------------------------------------------------------------------------

Window wnPerson 'Сотрудник';
  show at(,, 80, 7);

Screen scPerson ('',, sci178Esc);
  table mtPerson;
Fields
  mtPerson.FIO    (''): noprotect;
  mtPerson.Login  (''): noprotect;
  mtPerson.EMail  (''): noprotect;
  mtPerson.Passw  (''): noprotect;
  mtPerson.Sort   (''): noprotect;
<<

   `ФИО`             .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Логин`           .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `e-mail`          .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Пароль`          .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `N для сортировки`.@@@@@@@@@@@@
>>
End;

End;

WindowEvent wnPerson;

cmInit:
{
  var cGoto: comp;
  cGoto := 0;
  if isValid(#mtStruct)
    if (mtStruct.Mode = 2)
      cGoto := mtStruct.Rec;

  if (GetFirst mtPerson where (( cGoto == mtPerson.NRec )) = tsOk)
    RescanPanel(#mtPerson);
}

End;


private function DelPerson(_pers: string): boolean;
{
  result := false;

  if (GetFirst mtPersPer where (( _pers == mtPersPer.Person )) = tsOk)
    message('Удаление невозможно, так как есть результаты расчета по этому сотруднику.');
  else
    if (message('Удалить сотрудника?', Confirmation + YesNo) = cmYes)
    {
      delete mtAppoint where (( _pers == mtAppoint.Person ));
      delete mtPerson where (( _pers == mtPerson.Login ));
      result := true;
    }
}


TableEvent
  table mtPerson;

cmInsertRecord:
  insert current mtPerson;

cmUpdateRecord:
  update current mtPerson;

cmDeleteRecord:
  DelPerson(mtPerson.Login);

end;
