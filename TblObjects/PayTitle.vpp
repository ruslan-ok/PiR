//------------------------------------------------------------------------------
//                                                      (c) Корпорация Галактика
// Заработная плата по отделу
// Окно редактирования наименований начислений и выплат
//------------------------------------------------------------------------------

var cSelectedPayTitle: byte;
var sPayTitle: string;

Window wnPayTitle 'Наименование начисления или выплаты';
  show at(,, 80, 5);

Screen scPayTitle ('',, sci178Esc);
  table mtPayTitle;
Fields
  mtPayTitle.Id   (''): noprotect;
  mtPayTitle.Name (''): noprotect;
<<

   `Код`          .@@@@@@@@@@@@
   `Наименование` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
End;

End;

TableEvent
  table mtPayTitle;

cmInsertRecord:
  insert current mtPayTitle;

cmUpdateRecord:
  update current mtPayTitle;

cmDeleteRecord:
  if (GetFirst mtNachisl where (( mtPayTitle.Id == mtNachisl.Title )) = tsOk)
    message('Удаление невозможно, так как есть начисление с таким наименованием.');
  else
    if (GetFirst mtPayment where (( mtPayTitle.Id == mtPayment.Title )) = tsOk)
      message('Удаление невозможно, так как есть выплата с таким наименованием.');
    else
      if (message('Удалить запись?', Confirmation + YesNo) = cmYes)
        delete current mtPayTitle;

end;


Window wnPickPayTitle 'Выбор наименования' ('',, sci1478EnEsc) DoAccept;
  show at(,, 60, 15);

Browse brPayTitle;
  table mtPayTitle;
Fields
  mtPayTitle.Id   'Код':          [20], protect;
  mtPayTitle.Name 'Наименование': [40], protect;
End;

End;

WindowEvent wnPickPayTitle;

cmInit:
{
  PushPos(#mtPayTitle);
  ResetBounds(#mtPayTitle);

  if (GetFirst mtPayTitle where (( cSelectedPayTitle == mtPayTitle.Id )) = tsOk)
    RescanPanel(#mtPayTitle);

  cSelectedPayTitle := 0;
}

cmDefault:
{
  UpdateTable;
  cSelectedPayTitle := mtPayTitle.Id;
  set sPayTitle := mtPayTitle.Name;
}

cmDone:
{
  SetBounds(#mtPayTitle);
  PopPos(#mtPayTitle);
}

cmEdit:
  RunWindowModal(wnPayTitle);

cmInsert:
  PutCommand(cmEdit);

End;
