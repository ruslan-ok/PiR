//------------------------------------------------------------------------------
//                                                      (c) Корпорация Галактика
// Заработная плата по отделу
// Окно редактирования сотрудника
//------------------------------------------------------------------------------

Window wnPerson 'Сотрудник';
  show at(,, 100, 20);

Screen scPerson ('',, sci178Esc);
  show at(,,, 10) fixed_y;
  table mtPerson;
Fields
  mtPerson.FIO:   NoProtect;
  mtPerson.Login: NoProtect;
  mtPerson.Sort:  NoProtect;
  mtPerson.EMail: NoProtect;
  mtPerson.Passw: Protect;
  mtPerson.Born:  NoProtect;
  mtPerson.Age:   Skip;
  mtPerson.Phone: NoProtect;
  mtPerson.Addr:  NoProtect;
  mtPerson.Info:  NoProtect;
Buttons
  cmValue7;
<<

   `ФИО`          .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ `N для сортировки`
   `Логин`        .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  .@@@@@@@@@@@@
   `e-mail`       .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Пароль`       .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ <. Генерировать .>
   `Дата рождения`.@@@@@@@@@@@@   .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Телефон`      .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Адрес`        .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Коммент`      .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
End;

TabbedSheet bottom tsPersInfo;
  show at(, 11,,);

Browse brPersApp 'Назначения' ('',, sci1478Esc);
  table mtAppoint;
Fields
  mtAppoint.TabNum   'Таб. номер':     [10], noProtect;
  mtAppoint.dBeg     'Назначен с':     [15], NoProtect, {font = {backColor = if(mtAppoint.CorrectPer, 0, ColorError)}};
  mtAppoint.dEnd     'Контракт до':    [15], NoProtect;
  mtAppoint.Salary   'Оклад':          [13.2,'\2p[|-]366`666`666~99'], NoProtect;
  mtAppoint.Currency 'Вал.':           [ 5], NoProtect;
  mtDepart.Name      'Подразделение' ('',, sci13478Esc): [15], Protect;
  mtPost.Name        'Должность'     ('',, sci13478Esc): [25], Protect;
  mtAppoint.TaxDed   'Налог и валюта': [10], NoProtect;
End;

Browse brChild 'Дети' ('',, sci178Esc);
  table mtChild;
Fields
  mtChild.Name 'Имя':           [40], NoProtect;
  mtChild.Born 'Дата рождения': [15], NoProtect;
  mtChild.Num  'Номер':          [5], NoProtect;
  mtChild.Info 'Коммент':       [40], NoProtect;
End;

Browse brEducation 'Образование' ('',, sci178Esc);
  table mtEducation;
Fields
  mtEducation.dBeg          'Поступление':   [15], NoProtect;
  mtEducation.dEnd          'Окончание':     [15], NoProtect;
  mtEducation.Institution   'Учреждение':    [20], NoProtect;
  mtEducation.Course        'Курс':          [20], NoProtect;
  mtEducation.Specialty     'Специальность': [20], NoProtect;
  mtEducation.Qualification 'Квалификация':  [20], NoProtect;
  mtEducation.Document      'Тип документа': [10], NoProtect;
  mtEducation.Number        'Номер':         [10], NoProtect;
  mtEducation.City          'Город':         [10], NoProtect;
  mtEducation.DocDate       'Дата':          [15], NoProtect;
  mtEducation.Info          'Коммент':       [20], NoProtect;
End;

Browse brFioHist 'Фамилии' ('',, sci178Esc);
  table mtFioHist;
Fields
  mtFioHist.FIO  'Фамилия': [45], NoProtect;
  mtFioHist.dEnd 'Была до': [15], NoProtect;
  mtFioHist.Info 'Коммент': [40], NoProtect;
End;

End; // tsPersInfo

End; // wnPerson

procedure RunAppWin; forward;
function NextRandomCode(CodeLength: longInt): string; forward;

WindowEvent wnPerson;

cmDone:
  UpdateTable;

cmEdit:
  if (CurTable = #mtAppoint)
    RunAppWin;

cmValue7:
    if (message('Генерировать пароль для сотрудника ' + mtPerson.FIO + '?', Confirmation + YesNo) = cmYes)
      set mtPerson.Passw := NextRandomCode(20);

End;


Window wnPersList 'Список сотрудников';
  show at(,, 100, 20);

Browse brPerson ('',, sci1478Esc);
  table mtPerson;
Fields
  mtPerson.FIO   'ФИО':     [10], Protect;
  mtPerson.Years 'Лет':     [05], Protect;
  mtPerson.Login 'Логин':   [10], Protect;
  mtPerson.EMail 'e-mail':  [20], Protect;
  mtPerson.Passw 'Пароль':  [15], Protect;
  mtPerson.Sort  'Сорт.':   [05], Protect;
  mtPerson.Age   'Возраст': [15], Protect;
End;

End;

var bRestoreCond: boolean;

WindowEvent wnPersList;

cmInit:
{
  var cGoto: byte;
  cGoto := 0;
  if isValid(#mtStruct)
    if (mtStruct.Mode = 2)
      cGoto := mtStruct.Person;

  ResetBounds(#mtPerson);
  if (GetFirst mtPerson where (( cGoto == mtPerson.Id )) = tsOk)
    RescanPanel(#mtPerson);
}

cmDone:
  SetBounds(#mtPerson);

cmEdit:
  RunWindow(wnPerson);

End;



private function DelPerson(_pers: byte): boolean;
{
  result := false;

  if (GetFirst mtPersPer where (( _pers == mtPersPer.Person )) = tsOk)
    message('Удаление невозможно, так как есть результаты расчета по этому сотруднику.');
  else
    if (message('Удалить сотрудника?', Confirmation + YesNo) = cmYes)
    {
      delete mtAppoint where (( _pers == mtAppoint.Person ));
      delete mtPerson where (( _pers == mtPerson.Id ));
      result := true;
    }
}


TableEvent
  table mtPerson;

cmPositionChanged:
  if (mtPerson.Passw = '')
    EnableCommand(cmValue7);
  else
    DisableCommand(cmValue7);

cmInsertRecord:
  insert current mtPerson;

cmUpdateRecord:
  update current mtPerson;

cmDeleteRecord:
  DelPerson(mtPerson.Id);

end;


TableEvent
  table mtFioHist;

cmInsertRecord:
  insert current mtFioHist;

cmUpdateRecord:
  update current mtFioHist;

cmDeleteRecord:
  if (message('Удалить?', YesNo + Confirmation) = cmYes)
    delete current mtFioHist;

end;


TableEvent
  table mtChild;

cmInsertRecord:
  insert current mtChild;

cmUpdateRecord:
  update current mtChild;

cmDeleteRecord:
  if (message('Удалить?', YesNo + Confirmation) = cmYes)
    delete current mtChild;

end;


TableEvent
  table mtEducation;

cmInsertRecord:
  insert current mtEducation;

cmUpdateRecord:
  update current mtEducation;

cmDeleteRecord:
  if (message('Удалить?', YesNo + Confirmation) = cmYes)
    delete current mtEducation;

end;


// Генерация псевдослучайного кода
// (C) copyright C_Common\vip\CommonRandom.vip

var LastRandomCntr: comp; // счетчик псевдослучайной последовательности

const
  Symbols: string = 'ABCDEFGHIGHKLMNOPQRSTUWVXYZ0123456789'; // массив букв и цифр
end;

// функция генерации следующего числа псевдослучайной последовательности
// результат - случайное число от 0 до Range-1
function NextRandomNumber(Range: longInt): byte;
{
  LastRandomCntr := 1664525 * LastRandomCntr + 1013904223 - ((1664525 * LastRandomCntr + 1013904223) / 2147483647) * 2147483647 + _ToSec100(Cur_Time_NoAQA);
  result := byte(abs(LastRandomCntr - (LastRandomCntr/Range) * Range));
}

// функция генерации случайной последовательности букв и цифр длиною CodeLength
function NextRandomCode(CodeLength: longInt): string;
{
  var SymbolNum: longInt;

  result := '';

  for(SymbolNum := 1; SymbolNum <= CodeLength; SymbolNum++)
    result += Symbols[NextRandomNumber(Length(Symbols)) + 1];
}
