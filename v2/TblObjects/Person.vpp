//------------------------------------------------------------------------------
//                                                      (c) Корпорация Галактика
// Заработная плата по отделу
// Окно редактирования сотрудника
//------------------------------------------------------------------------------

Window wnPerson 'Сотрудник';
  show at(,, 100, 20);

Screen scPerson ('',, sci178Esc);
  show at(,,, 8) fixed_y;
  table mtPerson;
Fields
  mtPerson.FIO:   NoProtect;
  mtPerson.Login: NoProtect;
  mtPerson.EMail: NoProtect;
  mtPerson.Passw: NoProtect;
  mtPerson.Sort:  NoProtect;
  mtPerson.Born:  NoProtect;
  mtPerson.Info:  NoProtect;
<<

   `ФИО`             .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Логин`           .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `e-mail`          .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Пароль`          .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `N для сортировки`.@@@@@@@@@@@@              `Дата рождения`.@@@@@@@@@@@@
   `Коммент`         .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
End;

TabbedSheet bottom tsPersInfo;
  show at(, 9,,);

Browse brPersApp 'Назначения' ('',, sci178Esc);
  table mtAppoint;
Fields
  mtAppoint.TabNum   'Таб. номер':     [10], noProtect;
  mtAppoint.dBeg     'Назначен с':     [15], NoProtect, {font = {backColor = if(mtAppoint.CorrectPer, 0, ColorError)}};
  mtAppoint.dEnd     'Контракт до':    [15], NoProtect;
  mtAppoint.Salary   'Оклад':          [13.2,'\2p[|-]366`666`666~99'], NoProtect;
  mtAppoint.Currency 'Вал.':           [ 5], NoProtect;
  mtDepart.Name      'Подразделение' ('',, sci1378Esc): [15], Protect;
  mtPost.Name        'Должность'     ('',, sci1378Esc): [25], Protect;
  mtAppoint.TaxDed   'Налог и валюта': [10], NoProtect;
End;

Browse brChild 'Дети' ('',, sci178Esc);
  table mtChild;
Fields
  mtChild.Name 'Имя':           [40], NoProtect;
  mtChild.Born 'Дата рождения': [15], NoProtect;
  mtChild.Num  'Номер':          [5], NoProtect;
  mtChild.Info 'Коммент':       [40], NoProtect;
End;

Browse brEducation 'Образование' ('',, sci178Esc);
  table mtEducation;
Fields
  mtEducation.dBeg          'Поступление':   [15], NoProtect;
  mtEducation.dEnd          'Окончание':     [15], NoProtect;
  mtEducation.Institution   'Учреждение':    [20], NoProtect;
  mtEducation.Course        'Курс':          [20], NoProtect;
  mtEducation.Specialty     'Специальность': [20], NoProtect;
  mtEducation.Qualification 'Квалификация':  [20], NoProtect;
  mtEducation.Document      'Тип документа': [10], NoProtect;
  mtEducation.Number        'Номер':         [10], NoProtect;
  mtEducation.City          'Город':         [10], NoProtect;
  mtEducation.DocDate       'Дата':          [15], NoProtect;
  mtEducation.Info          'Коммент':       [20], NoProtect;
End;

Browse brFioHist 'Фамилии' ('',, sci178Esc);
  table mtFioHist;
Fields
  mtFioHist.FIO  'Фамилия': [45], NoProtect;
  mtFioHist.dEnd 'Была до': [15], NoProtect;
  mtFioHist.Info 'Коммент': [40], NoProtect;
End;

End; // tsPersInfo

End; // wnPerson


Window wnPersList 'Список сотрудников';
  show at(,, 100, 20);

Browse brPerson ('',, sci1478Esc);
  table mtPerson;
Fields
  mtPerson.FIO   'ФИО':    [10], NoProtect;
  mtPerson.Login 'Логин':  [10], NoProtect;
  mtPerson.EMail 'e-mail': [20], NoProtect;
  mtPerson.Passw 'Пароль': [15], NoProtect;
  mtPerson.Sort  'Сорт.':  [05], NoProtect;
End;

End;

var bRestoreCond: boolean;

WindowEvent wnPersList;

cmInit:
{
  bRestoreCond := false;
  if ConditionActiveInLeave(tcCurAppoint, tnmtAppoint)
  {
    bRestoreCond := true;
    PopConditionForLeave(tcCurAppoint, tnmtAppoint);
  }

  var cGoto: byte;
  cGoto := 0;
  if isValid(#mtStruct)
    if (mtStruct.Mode = 2)
      cGoto := mtStruct.Person;

  ResetBounds(#mtPerson);
  if (GetFirst mtPerson where (( cGoto == mtPerson.Id )) = tsOk)
    RescanPanel(#mtPerson);
}

cmDone:
{
  SetBounds(#mtPerson);
  if bRestoreCond
    PushConditionForLeave(tcCurAppoint, tnmtAppoint);
}

cmEdit:
  RunWindow(wnPerson);

End;



private function DelPerson(_pers: byte): boolean;
{
  result := false;

  if (GetFirst mtPersPer where (( _pers == mtPersPer.Person )) = tsOk)
    message('Удаление невозможно, так как есть результаты расчета по этому сотруднику.');
  else
    if (message('Удалить сотрудника?', Confirmation + YesNo) = cmYes)
    {
      delete mtAppoint where (( _pers == mtAppoint.Person ));
      delete mtPerson where (( _pers == mtPerson.Id ));
      result := true;
    }
}


TableEvent
  table mtPerson;

cmInsertRecord:
  insert current mtPerson;

cmUpdateRecord:
  update current mtPerson;

cmDeleteRecord:
  DelPerson(mtPerson.Id);

end;


TableEvent
  table mtFioHist;

cmInsertRecord:
  insert current mtFioHist;

cmUpdateRecord:
  update current mtFioHist;

cmDeleteRecord:
  if (message('Удалить?', YesNo + Confirmation) = cmYes)
    delete current mtFioHist;

end;


TableEvent
  table mtChild;

cmInsertRecord:
  insert current mtChild;

cmUpdateRecord:
  update current mtChild;

cmDeleteRecord:
  if (message('Удалить?', YesNo + Confirmation) = cmYes)
    delete current mtChild;

end;


TableEvent
  table mtEducation;

cmInsertRecord:
  insert current mtEducation;

cmUpdateRecord:
  update current mtEducation;

cmDeleteRecord:
  if (message('Удалить?', YesNo + Confirmation) = cmYes)
    delete current mtEducation;

end;


