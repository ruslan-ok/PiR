//------------------------------------------------------------------------------
//                                                      (c) Корпорация Галактика
// Заработная плата по отделу
// Импорт из xlsx
//------------------------------------------------------------------------------

var wbNum: longint = 0;
var lSheetsCount, lSheetNum: longint;
var sSheetName: string;
var row, col, maxRow, maxCol: longint = 0;
var sCell, sCell2: string = '';
var fCell: double;
var pcDefDepart, pcDefPost: byte;

private function OpenExcel: boolean;
{
  result := false;
  sSheetName := '';

  if (not xlOpenExcel(false))
  {
    wlog.LogLine(APE_ERROR, 'Не открылся Excel');
    exit;
  }

  if (not xlIsExcelValid)
  {
    wlog.LogLine(APE_ERROR, 'Не инициализирован Excel');
    xlKillExcel;
    exit;
  }

  xlGetWorkBooksCount(wbNum);

  if (not xlSetActiveWorkBookByName(sFile))
    if (not xlOpenWorkBook(sFile))
    {
      wlog.LogLine(APE_ERROR, 'Не открылась книга Excel ' + sFile);
      xlKillExcel;
      exit;
    }

  wbNum++;

  if (not xlGetSheetsCount(lSheetsCount))
  {
    wlog.LogLine(APE_ERROR, 'Не инициализирован Excel (ошибка вызова xlGetSheetsCount)');
    xlKillExcel;
    exit;
  }

  result := true;
}
/*
private function NeedChangeParam(_src, _dst, _name: string): boolean;
{
  result := false;

  if (_src = _dst)
    exit;

  if (_dst = string(word(0))) or (_dst = string(double(0))) or (_dst = string(ZeroDate))
    exit;

  result := true;

  if (wDetProt <> 0)
    if (_src = string(word(0))) or (_src = string(double(0))) or (_src = string(ZeroDate))
      wlog.LogLine(APE_INFO, _name + ' ' + _dst);
    else
      wlog.LogLine(APE_INFO, _name + ' ' + _dst + ' (было ' + _src + ')');
}
*/
private function CorrectBonusTitle(_title: string): string;
{
  result := _title;

  if (Pos('Проектный бизнес', _title) = 1)
    result := 'Проектный бизнес';

  if (Pos('Премия Отдела', _title) = 1)
    result := 'Премия отдела';

  if (Pos('Премия Управления', _title) = 1)
    result := 'Премия Управления';

  if (Pos('Межрасчеты', _title) = 1)
    result := 'Межрасчеты';

  if (Pos('З.пл', _title) = 1)
    result := 'Зарплата';
}

private function isNachisl(_name: string): boolean;
{
  result := (Pos('Премия', _name) = 1) or (Pos('Проектный бизнес', _name) = 1) or (Pos('Прочие премии', _name) = 1);
}

private function isPayment(_name: string): boolean;
{
  result := (Pos('Аванс', _name) = 1) or (Pos('Межрасчеты', _name) = 1) or (Pos('Зарплата', _name) = 1) or (Pos('2-я часть', _name) = 1);
}

private function GetPayTitle(_name: string): byte;
{
  result := 0;

  if isNachisl(_name) or isPayment(_name)
    result := GetTitleId(_name);
}

private function ReadColumns(var _PlanDays: byte; var _AvansDate, _PaymentDate, _Part2Date: date; var _AvansRate, _PaymentRate, _Part2Rate: double): boolean;
{
  result := false;

  delete all mtColTitle;
  delete all mtSpCol;

  xlGetCellValue(3, 3, sCell);

  if (sCell <> 'Окуневич Р.В.')
  {
    wlog.LogLine(APE_WARNING, 'В ячейке C3 ожидалось значение "Окуневич Р.В.", а обнаружено значение "' + sCell + '"');
    exit;
  }

  var lNum, lSp: longint = 1;
  var sMainTitle: string = '';
  row := 2;
  col := 1;

  do
  {
    xlGetCellValue(row-1, col, sCell);
    xlGetCellValue(row, col, sCell2);
    if (sCell <> '') or (sCell2 <> '')
    {
      if (sMainTitle = '') and (sCell = '') and (sCell2 <> '')
      {
        sCell2 := CorrectBonusTitle(sCell2);
        if (GetFirst mtColTitle where (( sCell2 == mtColTitle.Name )) <> tsOk)
        {
          ClearBuffer(#mtColTitle);
          mtColTitle.Num := lNum++;
          mtColTitle.Name := sCell2;
          mtColTitle.PayTitle := GetPayTitle(sCell2);
          insert current mtColTitle;
        }
      }

      var _name1, _name2: string;

      if (sCell <> '')
      {
        sMainTitle := sCell;

        _name1 := sCell;
        _name1 := Replace(_name1, ''#13#10, ' ');
        _name1 := Replace(_name1, ''#10, ' ');
        _name1 := Replace(_name1, ''#13, ' ');

        if (length(_name1) > 10)
        {
          var ss: string = '';
          ss := SubStr(_name1, length(_name1) - 9, 255);

          var dt: date = ZeroDate;
          dt := StrToDate(ss, 'DD.MM.YYYY');

          if (dt <> ZeroDate)
            _name1 := Trim(SubStr(_name1, 1, length(_name1) - 10));
        }

        _name1 := CorrectBonusTitle(_name1);

        if (GetFirst mtColTitle where (( _name1 == mtColTitle.Name )) <> tsOk)
        {
          ClearBuffer(#mtColTitle);
          mtColTitle.Num := lNum++;
          mtColTitle.Name := _name1;
          mtColTitle.PayTitle := GetPayTitle(_name1);
          insert current mtColTitle;
        }
        lSp := 1;
      }

      _name2 := sCell2;
      _name2 := Replace(_name2, ''#13#10, ' ');
      _name2 := Replace(_name2, ''#10, ' ');
      _name2 := Replace(_name2, ''#13, ' ');

      if (sMainTitle = '') and (sCell = '') and (sCell2 <> '')
        _name2 := '';

      if (GetFirst mtSpCol where (( mtColTitle.Num == mtSpCol.Col and
                                    _name2         == mtSpCol.Name )) = tsOk)
        lSp := mtSpCol.Num + 1;
      else
      {
        ClearBuffer(#mtSpCol);
        mtSpCol.Col := mtColTitle.Num;
        mtSpCol.Num := lSp++;
        mtSpCol.Name := _name2;
        mtSpCol.ColNum := col;
        insert current mtSpCol;
//        if (_name2 <> '')
//          wlog.LogLine(APE_INFO, 'Колонка: ' + mtColTitle.Name + if(mtSpCol.Name = '', '', ' - ' + mtSpCol.Name));
      }
    }
    col++;
  }
  while ((col < 50) and ((col < 40) or (sCell <> '') or (sCell2 <> '')));
  maxCol := col-1;

  row := 3;
  col := 3;

  do
  {
    xlGetCellValue(row, col, sCell);
    row++;
  }
  while (sCell <> '');
  maxRow := row-1;

  //----------------------------------------------
  var wAttr: word = 0;

  for (row := maxRow + 1; row <= maxRow + 20; row++)
  {
    var sName: string = '';
    xlGetCellValue(row, 1, sName);
    xlGetCellValue(row, 3, sCell);

    var wTmp: word;
    var dTmp: date;

    if (sName <> '')
    {
      if (sCell = '')
        wlog.LogLine(APE_WARNING, 'Нет значения для атрибута ' + sName);
      else
      {
        case sName of

          'план, дней':
          {
            _PlanDays := byte(sCell);
//            if NeedChangeParam(string(mtPeriod.PlanDays), string(wTmp), 'Количество дней')
//              mtPeriod.PlanDays := wTmp;
          }

          'аванс':
          {
            _AvansDate := StrToDate(sCell, 'DD.MM.YYYY');
//            if NeedChangeParam(string(mtPeriod.AvansDate), string(dTmp), 'Дата аванса')
//              mtPeriod.AvansDate := dTmp;
            wAttr := 1;
          }

          'з/п':
          {
            _PaymentDate := StrToDate(sCell, 'DD.MM.YYYY');
//            if NeedChangeParam(string(mtPeriod.PaymentDate), string(dTmp), 'Дата выплаты зарплаты')
//              mtPeriod.PaymentDate := dTmp;
            wAttr := 2;
          }

          '2-я часть':
          {
            _Part2Date := StrToDate(sCell, 'DD.MM.YYYY');
//            if NeedChangeParam(string(mtPeriod.PaymentDate), string(dTmp), 'Дата выплаты зарплаты')
//              mtPeriod.PaymentDate := dTmp;
            wAttr := 3;
          }

          'межрасчеты':
          {
          }

          'курс':
          {
            fCell := double(Replace(sCell, ',', '.'));

            if (fCell <> 0)
              case wAttr of

                1:
                {
                  _AvansRate := fCell;
//                  if NeedChangeParam(DoubleToStr(mtPeriod.AvansRate, '\4p[|-]366 666.8888'), DoubleToStr(fCell, '\4p[|-]366 666.8888'), 'Курс выплаты аванса')
//                    mtPeriod.AvansRate := fCell;
                }

                2:
                {
                  _PaymentRate := fCell;
//                  if NeedChangeParam(DoubleToStr(mtPeriod.PaymentRate, '\4p[|-]366 666.8888'), DoubleToStr(fCell, '\4p[|-]366 666.8888'), 'Курс выплаты зарплаты')
//                    mtPeriod.PaymentRate := fCell;
                }

                3:
                {
                  _Part2Rate := fCell;
//                  if NeedChangeParam(DoubleToStr(mtPeriod.PaymentRate, '\4p[|-]366 666.8888'), DoubleToStr(fCell, '\4p[|-]366 666.8888'), 'Курс выплаты зарплаты')
//                    mtPeriod.PaymentRate := fCell;
                }

              end;
            wAttr := 0;
          }

          'курс прошлого месяца', 'курс вход':
          {
          }

        else
          wlog.LogLine(APE_WARNING, 'На листе "' + sSheetName + '" новый атрибут периода "' + sName + '".');
        end;
      }
    }
  }

  //update current mtPeriod;
  result := (RecordsInTable(#mtColTitle) > 10) and (_PlanDays <> 0) and (_AvansDate <> ZeroDate) and (_PaymentDate <> ZeroDate) and (_AvansRate <> 0) and (_PaymentRate <> 0);
}


private procedure CheckPerson(_npp: string);
{
  sCell := Replace(sCell, 'Кузнецов Виталий А.', 'Кузнецов В.А.');
  sCell := Replace(sCell, '.  ', '.');
  sCell := Replace(sCell, '. ', '.');

  if (GetFirst mtPerson where (( sCell == mtPerson.FIO )) = tsOk)
    wlog.LogMessage(APE_INFO, sCell);
  else
  {
    ClearBuffer(#mtPerson);
    mtPerson.FIO   := sCell;
    mtPerson.Login := sCell;
    mtPerson.Sort  := LPadCh(_npp, '0', 6);
    if (GetFirst KatSotr where (( sCell == KatSotr.FIO )) = tsOk)
    {
      mtPerson.Login := KatSotr.Name;
      mtPerson.EMail := KatSotr.EMail;
    }
    insert current mtPerson;
    wlog.LogMessage(APE_INFO, sCell);
    wlog.LogLine(APE_WARNING, 'Добавлен сотрудник ' + sCell);
  }

  if (GetFirst mtPersPer where (( mtPerson.Id == mtPersPer.Person and
                                  mtPeriod.Id == mtPersPer.Period )) <> tsOk)
  {
    ClearBuffer(#mtPersPer);
    mtPersPer.Person := mtPerson.Id;
    mtPersPer.Period := mtPeriod.Id;
    insert current mtPersPer;
  }
}


private procedure CheckAppoint(_tab: string);
{
  if (GetLast mtAppoint where (( mtPerson.Id   == mtAppoint.Person and
                               ((mtPeriod.dEnd >= mtAppoint.dBeg) and
                               ((mtAppoint.dEnd = ZeroDate) or (mtAppoint.dEnd > mtPeriod.dBeg))) )) <> tsOk)
  {
    ClearBuffer(#mtAppoint);
    mtAppoint.Person := mtPerson.Id;
    mtAppoint.dBeg   := mtPeriod.dBeg;
    mtAppoint.dEnd   := ZeroDate;
    mtAppoint.Depart := pcDefDepart;
    mtAppoint.Post   := pcDefPost;
    mtAppoint.TabNum := _tab;
    mtAppoint.Salary := fCell;
    mtAppoint.Currency := 'USD';
    insert current mtAppoint;
    wlog.LogLine(APE_WARNING, 'Добавлено назначение сотрудника (оклад ' + sCell + ' USD)');
  }
  else
  {
    if (mtAppoint.Salary = fCell) and (mtAppoint.Salary <> 0)
    {
      if (_tab <> '') and (mtAppoint.TabNum = '')
      {
        mtAppoint.TabNum := _tab;
        mtAppoint.Salary := fCell;
        update current mtAppoint;
        wlog.LogLine(APE_INFO, 'Табельный номер: ' + _tab);
      }
    }
    else
      if (mtAppoint.Currency = 'USD')
      {
        var dContract: date = mtAppoint.dEnd;

        mtAppoint.dEnd := Sub_Day(mtPeriod.dBeg, 1);
        update current mtAppoint;

        wlog.LogLine(APE_WARNING, 'Добавлено назначение сотрудника в связи с изменением оклада с ' + string(mtAppoint.Salary) + ' на ' + sCell + ' USD');
        mtAppoint.Salary := fCell;
        mtAppoint.dBeg := mtPeriod.dBeg;
        mtAppoint.dEnd := dContract;
        insert current mtAppoint;
      }
  }
}

private procedure CheckFactDays;
{
  if (GetFirst mtPersPer where (( mtPerson.Id == mtPersPer.Person and
                                  mtPeriod.Id == mtPersPer.Period )) <> tsOk)
  {
    ClearBuffer(#mtPersPer);
    mtPersPer.Person := mtPerson.Id;
    mtPersPer.Period := mtPeriod.Id;
    mtPersPer.FactDays := byte(sCell);
    insert current mtPersPer;
  }
  else
    if (mtPersPer.FactDays <> byte(sCell))
    {
      wlog.LogLine(APE_INFO, 'Факт, дней: ' + sCell + if(mtPersPer.FactDays = 0, '', ' (было ' + string(mtPersPer.FactDays) + ')'));
      mtPersPer.FactDays := byte(sCell);
      update current mtPersPer;
    }
}

private procedure CheckNachisl;
{
  if (sCell = '') or (mtColTitle.PayTitle = 0)
    exit;

  var fValue: double = fCell;
  var yCurr: byte = cgUSD;
  var fInBYN: double = fValue/0.91*mtPeriod.PaymentRate;
  var sInfo: string = '';

  if (mtPerson.Login = 'IP') and (fInBYN >= 299) and (fInBYN <= 301)
  {
    fValue := 273;
    yCurr := cgBYN;
    sInfo := 'Персональная надбавка';
  }

  if (GetFirst mtNachisl where (( mtPerson.Id == mtNachisl.Person and
                                  mtPeriod.Id == mtNachisl.Period and
                          mtColTitle.PayTitle == mtNachisl.Title )) = tsOk)
  {
    if (mtNachisl.Value <> fValue) or (mtNachisl.Currency <> yCurr) or (mtNachisl.Rate <> mtPeriod.PaymentRate)
    {
      wlog.LogLine(APE_INFO, mtColTitle.Name + ': ' + string(fValue) + ' ' + if(yCurr = cgUSD, 'USD', 'BYN') + ' (было ' + string(mtNachisl.Value) + ' ' + if(yCurr = cgUSD, 'USD', 'BYN') + ')');
      mtNachisl.Value    := fValue;
      mtNachisl.Currency := yCurr;
      mtNachisl.Rate     := mtPeriod.PaymentRate;
      if (mtNachisl.Info = '')
        mtNachisl.Info     := sInfo;
      update current mtNachisl;
    }
  }
  else
  {
    wlog.LogLine(APE_INFO, mtColTitle.Name + ': ' + string(fValue) + ' ' + if(yCurr = cgUSD, 'USD', 'BYN'));
    mtNachisl.Person   := mtPerson.Id;
    mtNachisl.Period   := mtPeriod.Id;
    mtNachisl.Title    := mtColTitle.PayTitle;
    mtNachisl.Value    := fValue;
    mtNachisl.Currency := yCurr;
    mtNachisl.Rate     := mtPeriod.PaymentRate;
    mtNachisl.Info     := sInfo;
    insert current mtNachisl;
  }
}

private procedure CheckDebtIn;
{
  if (fCell = 0)
    exit;

  if (mtPersPer.DebtIn <> fCell)
  {
    wlog.LogLine(APE_INFO, mtColTitle.Name + ': ' + sCell + ' BYN (было ' + string(mtPersPer.DebtIn) + ' BYN)');
    mtPersPer.DebtIn := fCell;
    update current mtPersPer;
  }
}

private procedure CheckPayment(_value, _rate: double; _payed: date);
{
  if (_value = 0) or (mtColTitle.PayTitle = 0)
    exit;

  if (GetFirst mtPayment where (( mtPerson.Id == mtPayment.Person and
                                  mtPeriod.Id == mtPayment.Period and
                          mtColTitle.PayTitle == mtPayment.Title )) = tsOk)
  {
    if (mtPayment.Value <> _value) or (mtPayment.Currency <> cgBYN) or (mtPayment.Rate <> _rate) or (mtPayment.Payed <> _payed)
    {
      wlog.LogLine(APE_INFO, mtColTitle.Name + ': ' + string(_value) + ' BYN (было ' + string(mtPayment.Value) + ' BYN)');
      mtPayment.Value    := _value;
      mtPayment.Currency := cgBYN;
      mtPayment.Rate     := _rate;
      mtPayment.Payed    := _payed;
      update current mtPayment;
    }
  }
  else
  {
    wlog.LogLine(APE_INFO, mtColTitle.Name + ': ' + string(_value) + ' BYN');
    mtPayment.Person   := mtPerson.Id;
    mtPayment.Period   := mtPeriod.Id;
    mtPayment.Title    := mtColTitle.PayTitle;
    mtPayment.Value    := _value;
    mtPayment.Currency := cgBYN;
    mtPayment.Rate     := _rate;
    mtPayment.Payed    := _payed;
    insert current mtPayment;
  }
}

private procedure CheckDebtOut;
{
  if (fCell = 0)
    exit;

  if (mtPersPer.DebtOut <> fCell)
  {
    wlog.LogLine(APE_INFO, mtColTitle.Name + ': ' + sCell + ' BYN (было ' + string(mtPersPer.DebtOut) + ' BYN)');
    mtPersPer.DebtOut := fCell;
    update current mtPersPer;
  }
}

private procedure ReadPersons;
{
  row := 3;
  var bHaveData: boolean = true;

  while bHaveData
  {
    var sNpp, sTab: string = '';

    _loop mtColTitle
    {
      if not bHaveData
        break;

      if (mtColTitle.Name = 'Начислено') or
         (mtColTitle.Name = 'К выдаче "чистые" + вход. остаток') or
         (mtColTitle.Name = 'Вычеты') or
         (mtColTitle.Name = 'Льгота') or
         (mtColTitle.Name = 'Доход') or
         (mtColTitle.Name = 'Аванс и межрасчет') or
         (mtColTitle.Name = 'К выплате') or
         (mtColTitle.Name = 'Мой расчет зачета на след. месяц') or
         (mtColTitle.Name = 'Расхождение с распечаткой, BYN') or
         (mtColTitle.Name = 'Расхождение с распечаткой')
        continue;

      if (mtColTitle.Name = 'таб. №')
        if (GetFirst mtSpCol where (( mtColTitle.Num == mtSpCol.Col )) <> tsOk)
          wlog.LogLine(APE_WARNING, 'Для колонки [' + mtColTitle.Name + '] не найдена спецификация.');
        else
        {
          xlGetCellValue(row, mtSpCol.ColNum, sTab);
          continue;
        }

      if (mtColTitle.Name = '№')
        if (GetFirst mtSpCol where (( mtColTitle.Num == mtSpCol.Col )) <> tsOk)
          wlog.LogLine(APE_WARNING, 'Для колонки [' + mtColTitle.Name + '] не найдена спецификация.');
        else
        {
          xlGetCellValue(row, mtSpCol.ColNum, sNpp);
          continue;
        }

      if (mtColTitle.Name = 'ФИО') or (mtColTitle.Name = 'Отдел разработки "Управление персоналом"')
        if (GetFirst mtSpCol where (( mtColTitle.Num == mtSpCol.Col )) <> tsOk)
          wlog.LogLine(APE_WARNING, 'Для колонки [' + mtColTitle.Name + '] не найдена спецификация.');
        else
        {
          xlGetCellValue(row, mtSpCol.ColNum, sCell);
          bHaveData := (sCell <> '') and (sCell <> 'ИТОГО');
          if bHaveData
            CheckPerson(sNpp);
          continue;
        }

      if (Pos('оклад', mtColTitle.Name) > 0)
        if (GetFirst mtSpCol where (( mtColTitle.Num == mtSpCol.Col )) <> tsOk)
          wlog.LogLine(APE_WARNING, 'Для колонки [' + mtColTitle.Name + '] не найдена спецификация.');
        else
        {
          xlGetCellValue(row, mtSpCol.ColNum, sCell);
          fCell := double(Replace(sCell, ',', '.'));
          CheckAppoint(sTab);
          continue;
        }

      if (mtColTitle.Name = 'Факт, дней') or (mtColTitle.Name = 'Фактически отработано дней')
        if (GetFirst mtSpCol where (( mtColTitle.Num == mtSpCol.Col )) <> tsOk)
          wlog.LogLine(APE_WARNING, 'Для колонки [' + mtColTitle.Name + '] не найдена спецификация.');
        else
        {
          xlGetCellValue(row, mtSpCol.ColNum, sCell);
          CheckFactDays;
          continue;
        }

      if isNachisl(mtColTitle.Name)
        if (GetFirst mtSpCol where (( mtColTitle.Num      == mtSpCol.Col and
                                      'в валюте "чистая"' == mtSpCol.Name )) <> tsOk)
          wlog.LogLine(APE_WARNING, 'Для колонки [' + mtColTitle.Name + '] не найдена спецификация.');
        else
        {
          xlGetCellValue(row, mtSpCol.ColNum, sCell);
          fCell := double(Replace(sCell, ',', '.'));
          CheckNachisl;
          continue;
        }

      if (mtColTitle.Name = 'Зачет с прошлого месяца')
        if (GetFirst mtSpCol where (( mtColTitle.Num == mtSpCol.Col and
                                      'BYN'          == mtSpCol.Name )) <> tsOk)
          wlog.LogLine(APE_WARNING, 'Для колонки [' + mtColTitle.Name + '] не найдена спецификация.');
        else
        {
          xlGetCellValue(row, mtSpCol.ColNum, sCell);
          fCell := double(Replace(sCell, ',', '.'));
          CheckDebtIn;
          continue;
        }

      if isPayment(mtColTitle.Name)
        if (GetFirst mtSpCol where (( mtColTitle.Num == mtSpCol.Col and
                                      'Выдано, BYN'  == mtSpCol.Name )) <> tsOk)
          wlog.LogLine(APE_WARNING, 'Для колонки [' + mtColTitle.Name + '] не найдена спецификация.');
        else
        {
          xlGetCellValue(row, mtSpCol.ColNum, sCell);
          fCell := double(Replace(sCell, ',', '.'));
          var dPayed: date = ZeroDate;
          var fRate: double = 0;
          case SubStr(mtColTitle.Name, 1, 5) of

            'Аванс':
            {
              dPayed := mtPeriod.AvansDate;
              fRate := mtPeriod.AvansRate;
            }

            'Межра':
            {
              dPayed := ZeroDate;
              fRate := 0;
              var sTmp: string = '';

              if (GetFirst mtSpCol where (( mtColTitle.Num == mtSpCol.Col and
                                            'Дата выплаты' == mtSpCol.Name )) = tsOk)
              {
                xlGetCellValue(row, mtSpCol.ColNum, sTmp);
                dPayed := StrToDate(sTmp, 'DD.MM.YYYY');
              }
              if (dPayed = ZeroDate)
                dPayed := mtPeriod.AvansDate;

              if (GetFirst mtSpCol where (( mtColTitle.Num == mtSpCol.Col and
                                            'Курс выдачи'  == mtSpCol.Name )) = tsOk)
              {
                xlGetCellValue(row, mtSpCol.ColNum, sTmp);
                fRate := double(Replace(sTmp, ',', '.'));
              }
              if (fRate = 0)
                fRate := mtPeriod.AvansRate;
            }

            'Зарпл', 'З.пл':
            {
              dPayed := mtPeriod.PaymentDate;
              fRate := mtPeriod.PaymentRate;
            }

            '2-я ч':
            {
              dPayed := mtPeriod.Part2Date;
              fRate := mtPeriod.Part2Rate;
            }

          end;

          CheckPayment(fCell, fRate, dPayed);
          continue;
        }

      if (mtColTitle.Name = 'Зачет на следующий месяц, BYN')
        if (GetFirst mtSpCol where (( mtColTitle.Num == mtSpCol.Col )) <> tsOk)
          wlog.LogLine(APE_WARNING, 'Для колонки [' + mtColTitle.Name + '] не найдена спецификация.');
        else
        {
          xlGetCellValue(row, mtSpCol.ColNum, sCell);
          fCell := double(Replace(sCell, ',', '.'));
          CheckDebtOut;
          continue;
        }

      wlog.LogLine(APE_WARNING, 'Не обработана колонка [' + mtColTitle.Name + ']');
    }
    row++;
    if bHaveData
      wlog.LogCommit;
  }
}

private procedure CheckDepAndPost;
{
  pcDefDepart := pcDefPost := 0;

  ResetBounds(#mtDepart);
  if (GetFirst mtDepart <> tsOk)
  {
    ClearBuffer(#mtDepart);
    mtDepart.Name := 'ОРУП Атл';
    insert current mtDepart;
    pcDefDepart := mtDepart.Id;
    wlog.LogLine(APE_INFO, 'Добавлено подразделение "ОРУП Атл"');

    ClearBuffer(#mtDepHist);
    mtDepHist.dBeg := date(1,1,2000);
    mtDepHist.Sort := '001';
    mtDepHist.Depart := pcDefDepart;
    insert current mtDepHist;
  }
  else
    if (GetFirst mtDepart where (( 'ОРУП Атл' == mtDepart.Name )) = tsOk)
      pcDefDepart := mtDepart.Id;
  SetBounds(#mtDepart);

  ResetBounds(#mtPost);
  if (GetFirst mtPost <> tsOk)
  {
    ClearBuffer(#mtPost);
    mtPost.Name := 'инженер-программист';
    insert current mtPost;
    pcDefPost := mtPost.Id;
    wlog.LogLine(APE_INFO, 'Добавлена должность "инженер-программист"');
  }
  else
    if (GetFirst mtPost where (( 'инженер-программист' == mtPost.Name )) = tsOk)
      pcDefPost := mtPost.Id;
  SetBounds(#mtPost);
}

var
  bSelected: boolean;
  yPlanDays: byte;
  dAvansDate, dPaymentDate, dPart2Date: date;
  fAvansRate, fPaymentRate, fPart2Rate: double;

Window wnGetPeriodParam 'Параметры периода';
  show at(,, 70, 9);

Screen scPeriodParam ('',, sci1EnEsc);
  NoTableNavigation;
Fields
  yPlanDays:    NoProtect, {font = {backcolor = if(yPlanDays = 0, ColorNeed, 0)}};
  dAvansDate:   NoProtect, {font = {backcolor = if(dAvansDate = ZeroDate, ColorNeed, 0)}};
  fAvansRate:   [13.4,'\4p[|-]366`666~9999'], NoProtect, {font = {backcolor = if(fAvansRate = 0, ColorNeed, 0)}};
  dPaymentDate: NoProtect, {font = {backcolor = if(dPaymentDate = ZeroDate, ColorNeed, 0)}};
  fPaymentRate: [13.4,'\4p[|-]366`666~9999'], NoProtect, {font = {backcolor = if(fPaymentRate = 0, ColorNeed, 0)}};
  dPart2Date:   NoProtect;
  fPart2Rate:   [13.4,'\4p[|-]366`666~9999'], NoProtect;
Buttons
  cmOk, default;
  cmCancel;
<<

  `Плановое количество дней` .@@@@
                                               <. Продолжить .>
                  `Дата`          `Курс`
  `Аванс`     .@@@@@@@@@@@@@  .@@@@@@@@@@@@@   <.   Отмена   .>
  `Зарплата`  .@@@@@@@@@@@@@  .@@@@@@@@@@@@@
  `2-я часть` .@@@@@@@@@@@@@  .@@@@@@@@@@@@@
>>
End;

End; // wnGetPeriodParam

WindowEvent wnGetPeriodParam;

cmInit:
  bSelected := false;

cmOk:
{
  if (yPlanDays = 0) or (dAvansDate = ZeroDate) or (dPaymentDate = ZeroDate) or (fAvansRate = 0) or (fPaymentRate = 0)
  {
    message('Не все параметры указаны');
    exit;
  }

  bSelected := true;
  CloseWindow(wnGetPeriodParam);
}

End;

private procedure DoImportXlsx;
{
  StartNewVisual(vtRotateVisual, vfTimer + vfConfirm, 'Открытие файла...', 1);
  var bOk: boolean;
  bOk := OpenExcel;
  StopVisual;

  if not bOk
  {
    wlog.LogCommit;
    exit;
  }

  CheckDepAndPost;

  wlog.LogStartVisual('Чтение данных из файла ' + sFile, lSheetsCount);

  bOk := false;
  var bBreaked: boolean = false;

  for (lSheetNum := lSheetsCount; lSheetNum >= 1; lSheetNum--)
  {
    if not wlog.LogNextVisual(sSheetName)
    {
      bBreaked := true;
      break;
    }

    xlSetActiveSheet(lSheetNum);
    xlGetSheetName(lSheetNum, sSheetName);

    if (sSheetName = 'Курсы')
      continue;

    var m: byte = 0;
    var y: word = 0;

    if (sSheetName = 'Лист1')
    {
      var sPath, sName, sExt: string;
      ParseFileName(sFile, sPath, sName, sExt);

      if (Pos('Распечатка', sName) = 1)
      {
        sName := SubStr(sName, length(sName) - 6, 255);
        m := byte(SubStr(sName, 1, 2));
        y := word(SubStr(sName, 4, 4));
      }
    }

    if (m = 0) and (y = 0)
    {
      m := byte(SubStr(sSheetName, 1, 2));
      y := word(SubStr(sSheetName, 4, 4));
    }

    if (m >= 1) and (m <= 12) and (y > 2000)
    {
      var dPeriod: date;
      dPeriod := date(1, m, y);

      wlog.LogMessage(APE_INFO, DateToStr(dPeriod, 'MM.YYYY'));

      bOk := ReadColumns(yPlanDays, dAvansDate, dPaymentDate, dPart2Date, fAvansRate, fPaymentRate, fPart2Rate);

      if (yPlanDays = 0)
        wlog.LogLine(APE_ERROR, 'Не удалось прочитать плановое количество дней.');

      if (dAvansDate = ZeroDate)
        wlog.LogLine(APE_ERROR, 'Не удалось прочитать дату аванса.');

      if (dPaymentDate = ZeroDate)
        wlog.LogLine(APE_ERROR, 'Не удалось прочитать дату выплаты.');

      if (fAvansRate = 0)
        wlog.LogLine(APE_ERROR, 'Не удалось прочитать курс на дату аванса.');

      if (fPaymentRate = 0)
        wlog.LogLine(APE_ERROR, 'Не удалось прочитать курс на дату выплаты.');

      if (not bOk) and (RecordsInTable(#mtColTitle) > 10)
      {
        RunWindowModal(wnGetPeriodParam);
        //bOk := bSelected;
      }

      if bOk
      {
        var bNewPer: boolean;
        bNewPer := (GetFirst mtPeriod where (( dPeriod == mtPeriod.dBeg )) <> tsOk);

        if bNewPer
        {
          ClearBuffer(#mtPeriod);
          mtPeriod.dBeg := dPeriod;
          mtPeriod.PlanDays := yPlanDays;
          mtPeriod.AvansDate := dAvansDate;
          mtPeriod.PaymentDate := dPaymentDate;
          mtPeriod.Part2Date := dPart2Date;
          mtPeriod.AvansRate := fAvansRate;
          mtPeriod.PaymentRate := fPaymentRate;
          mtPeriod.Part2Rate := fPart2Rate;
          insert current mtPeriod;
          wlog.LogLine(APE_WARNING, 'Добавлен период: ' + string(yPlanDays) + ' дней, ' +
                                    string(dAvansDate) + ' аванс по курсу ' + DoubleToStr(fAvansRate, '\4p[|-]366 666.8888') + ', ' +
                                    string(dPaymentDate) + ' выплата зарплаты по курсу ' + DoubleToStr(fPaymentRate, '\4p[|-]366 666.8888') +
                                    if(dPart2Date = ZeroDate and fPart2Rate = 0, '',
                                       ', ' + string(dPart2Date) + ' выплата 2-й части по курсу ' + DoubleToStr(fPart2Rate, '\4p[|-]366 666.8888')));
        }
        else
        {
          if (mtPeriod.PlanDays <> yPlanDays) or (mtPeriod.AvansDate <> dAvansDate) or (mtPeriod.PaymentDate <> dPaymentDate) or
             (mtPeriod.AvansRate <> fAvansRate) or (mtPeriod.PaymentRate <> fPaymentRate) or (mtPeriod.Part2Date <> dPart2Date) or (mtPeriod.Part2Rate <> fPart2Rate)
          {
            var mess: string = '';

            if (mtPeriod.PlanDays <> yPlanDays)
            {
              mess := string(yPlanDays) + ' дней (было ' + string(mtPeriod.PlanDays) + ')';
              mtPeriod.PlanDays := yPlanDays;
            }

            if (mtPeriod.AvansDate <> dAvansDate) or (mtPeriod.AvansRate <> fAvansRate)
            {
              mess += if(mess = '', '', ', ');
              mess += string(dAvansDate) + ' аванс по курсу ' + DoubleToStr(fAvansRate, '\4p[|-]366 666.8888') +
                      ' (было ' + string(mtPeriod.AvansDate) + ' аванс по курсу ' + DoubleToStr(mtPeriod.AvansRate, '\4p[|-]366 666.8888') + ')';
              mtPeriod.dBeg := dPeriod;
              mtPeriod.AvansDate := dAvansDate;
              mtPeriod.AvansRate := fAvansRate;
            }

            if (mtPeriod.PaymentDate <> dPaymentDate) or (mtPeriod.PaymentRate <> fPaymentRate)
            {
              mess += if(mess = '', '', ', ');
              mess += string(dPaymentDate) + ' выплата зарплаты по курсу ' + DoubleToStr(fPaymentRate, '\4p[|-]366 666.8888') +
                      ' (было ' + string(mtPeriod.PaymentDate) + ' выплата зарплаты по курсу ' + DoubleToStr(mtPeriod.PaymentRate, '\4p[|-]366 666.8888') + ')';
              mtPeriod.dBeg := dPeriod;
              mtPeriod.PaymentDate := dPaymentDate;
              mtPeriod.PaymentRate := fPaymentRate;
            }

            if (mtPeriod.Part2Date <> dPart2Date) or (mtPeriod.Part2Rate <> fPart2Rate)
            {
              mess += if(mess = '', '', ', ');
              mess += string(dPart2Date) + ' выплата 2-й части по курсу ' + DoubleToStr(fPart2Rate, '\4p[|-]366 666.8888') +
                      ' (было ' + string(mtPeriod.Part2Date) + ' выплата 2-й по курсу ' + DoubleToStr(mtPeriod.Part2Rate, '\4p[|-]366 666.8888') + ')';
              mtPeriod.dBeg := dPeriod;
              mtPeriod.Part2Date := dPart2Date;
              mtPeriod.Part2Rate := fPart2Rate;
            }

            update current mtPeriod;
            wlog.LogLine(APE_WARNING, 'Скорректирован период: ' + mess);
          }
        }

        ReadPersons;
      }

      wlog.LogCommit;
    }
  }
  if (not bOk)
    wlog.LogLine(APE_WARNING, 'Не обнаружены листы с наименованием в формате MM.YYYY');

  wlog.LogStopVisual;

  xlCloseWorkBook(wbNum);
  xlKillExcel;
  wlog.Refresh;
  wlog.Visible := true;
}
